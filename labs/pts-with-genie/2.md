## Step 2. Execute Genie PTS


0. Activate your Python virtual environment

[Option] Run Docker container and activate your Python virtual environment inside it

```
# Run Docker container
[developer@devbox ~]$ docker run -it -v ~/genie_learning_lab:/root/genie_learning_lab ciscotestautomation/pyats:latest-robot /bin/bash

# Activate Python virtual environment
root@152b1e2e50a1:/pyats# source /pyats/bin/activate
```
- OR -

[Option] Activate Python virtual environment directly on DevNet Sandbox

```
# Activate Python virtual environment
[developer@devbox ~]$ source ~/pyats/bin/activate
```

> Note: The commands above have been executed within the `intro-to-genie` lab in Step #4 and don't need to be repeated if previously completed.


1. Navigate to the genie_learning_lab folder and view the script `genie_pts.robot`

```
cd ~/genie_learning_lab
cat genie_pts.robot
```


2. Examine the contents of the script `genie_pts.robot`

```
# Genie Devnet Lab-4: Running Genie PTS
# =====================================


*** Settings ***
Library        ats.robot.pyATSRobot
Library        genie.libs.robot.GenieRobot
Library        unicon.robot.UniconRobot


*** Variables ***
# Defining variables that can be used elsewhere in the test data.
${testbed}     default_testbed.yaml


*** Test Cases ***
# Creating testcases using available Genie, pyATS & Unicon keywords


# Connect to UUT and helper device using CLI
connect to UUT and helper device
    use genie testbed "${testbed}"
    connect to devices "uut ; helper"


# Run PTS for features BGP and OSPF on all devices
# Save it to local variable "pre_trigger_snapshot"
profile bgp & ospf on all devices as pre-trigger-snapshot
    Profile the system for "bgp;ospf" on devices "uut;helper" as "pre_trigger_snapshot"


# Execute a trigger on UUT
execute TriggerShutNoShutBgp on uut
    run trigger "TriggerShutNoShutBgp" on device "uut"


# Run PTS for features BGP and OSPF on all devices
# Save it to local variable "post_trigger_snapshot"
profile bgp & ospf on all devices as post-trigger-snapshot
    Profile the system for "bgp;ospf" on devices "uut;helper" as "post_trigger_snapshot"


# Compare pre_trigger_snapshot with pre_trigger_snapshot
compare pre_trigger_snapshot to pre-trigger-snapshot
    Compare profile "post_trigger_snapshot" with "pre_trigger_snapshot" on devices "uut;helper"

```

In the first test, we are running the PTS on all devices in the topology for features BGP and OSPF. This system profile snapshot is then saved to a local variable "pre_trigger_snapshot".

We then proceed to perform some action on the device that could potentially change the operational state of the topology. In the example above, we execute a trigger to shutdown and then unshut the BGP instance.

Following execution of the trigger, we run PTS on all devices in the topology for features BGP and OSPF again. This system profile snapshot is now saved to a local variable "post_trigger_snapshot". Using Genie keywords, we can then compare the Python datastructures stored within these 2 variables to validate if the device operational states before and after execution of the trigger stayed the same.

For more information, refer to the documentation on [Genie System Profiling with Robot](https://pubhub.devnetcloud.com/media/pyats-packages/docs/genie/robot/index.html#system-profiling)


3. Run this script by executing the following command

```
cd ~/genie_learning_lab
robot genie_pts.robot
```

Examine the output to see testcase results, HTML log report and XML output

```
(pyats) root@2c9d761472c2:~/genie_learning_lab# robot genie_pts.robot
==============================================================================
Genie Pts
==============================================================================
connect to UUT and helper device                                      | PASS |
------------------------------------------------------------------------------
profile bgp & ospf on all devices as pre-trigger-snapshot             | PASS |
------------------------------------------------------------------------------
execute TriggerShutNoShutBgp on uut                                   | PASS |
TriggerShutNoShutBgp has passed
------------------------------------------------------------------------------
profile bgp & ospf on all devices as post-trigger-snapshot            | PASS |
------------------------------------------------------------------------------
compare pre_trigger_snapshot to pre-trigger-snapshot                  | PASS |
Comparison between post_trigger_snapshot and pre_trigger_snapshot is identical
------------------------------------------------------------------------------
Genie Pts                                                             | PASS |
5 critical tests, 5 passed, 0 failed
5 tests total, 5 passed, 0 failed
==============================================================================
Output:  /root/genie_learning_lab/output.xml
Log:     /root/genie_learning_lab/log.html
Report:  /root/genie_learning_lab/report.html

```

View the log and report files created above as mentioned in the 'intro-to-genie' lab.

Note:
- This script demonstrates how Genie PTS can be used to report any operational state changes that may have occured as a consequence of actions performed on a device (in this case a `Trigger`). 

- The snapshot before the trigger had a memory_usage value of 1328 under address-family 'ipv4 unicast'. 

- After executing a `Trigger` (or any other potentially destructive action such as Reload, Switchover, etc.), the Genie PTS can be executed once again and operational state saved once again.

- Following this, the 2 snapshots (before and after the trigger) can be compared to check for changes.


#### Congratulations! You've Completed PTS with Genie!
