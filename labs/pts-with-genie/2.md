## Step 2. Execute Genie PTS


0. Run Docker container and activate your Python virtual environment inside it

```
# Run Docker container
docker run -it -v /genie_learning_labs:/genie_tests ciscotestautomation/pyats bash

# Once inside the Docker container, activate Python virtual environment
cd /pyats
source env.csh
```

Please note: This step is optional if you still have the Docker container launched in `intro-to-genie` lab [Step4]()


1. Create a new file genie_pts.robot

```
cd genie_tests
touch genie_pts.robot
```


2. Edit the file genie_pts.robot and copy the code below into it

```
# Example
# -------
# Genie Devnet Lab-5: Running Genie PTS

*** Settings ***
Library        pyats.robot.pyATSRobot
Library        genie.libs.robot.GenieRobot
Library        unicon.robot.UniconRobot

*** Variables ***
# Defining variables that can be used elsewhere in the test data.
${testbed}     /genie_tests/default_testbed.yaml
${datafile}    ${VIRTUAL_ENV}/genie_yamls/pts_datafile.yaml
${golden_pts}  /path/to/previously/saved/pts.json

*** Test Cases ***
# Creating test cases from available keywords.

# Connect to the device
connect to device uut
    use genie testbed "${testbed}"
    connect to devices "uut"

# Run PTS on all devices in the testbed and save it to location $golden_pts to be referenced later
# This will run PTS and save it to the location specified above
profile bgp & ospf on All and save it as golden PTS
    Profile the system for "bgp;ospf" on devices "nx-osv-1;csr1000v-1" at "$golden_pts"

# Run PTS on all devices, compare it to PTS saved at $golden_pts, save it to local variable pre_trigger_snapshot
profile bgp & ospf on All as pre trigger snapshot and compare to golden PTS
    Profile the system for "bgp;ospf" on devices "nx-osv-1;csr1000v-1" at "pre_trigger_snapshot"
    Compare profile "${golden_pts}" with "pre_trigger_snapshot" on devices "nx-osv-1;csr1000v-1"

# Execute some action that could impact device operational state
execute TriggerShutNoShutBgp
    run trigger "TriggerShutNoShutBgp" on device "uut"

# Run PTS on all devices and save it to local variable post_trigger_snapshot and compare it with pre_trigger_snapshot
profile bgp & ospf on All as post trigger snapshot and compare to pre trigger snapshot
    Profile the system for "bgp;ospf" on devices "nx-osv-1;csr1000v-1" at "post_trigger_snapshot"
    Compare profile "post_trigger_snapshot" with "pre_trigger_snapshot" on devices "nx-osv-1;csr1000v-1"
```


2. Run this script by executing the following command and examine the output to see testcase results, HTML log report and XML output.

```
(pyenv) hostname$ robot genie_pts.robot
====================================================================================================
Pyats                                                                         
====================================================================================================
connect to device uut                                                                       | PASS |
----------------------------------------------------------------------------------------------------
profile bgp & ospf on All and save it as golden PTS                                         | PASS |
----------------------------------------------------------------------------------------------------
profile bgp & ospf on All as pre trigger snapshot and compare to golden PTS                 | PASS |
----------------------------------------------------------------------------------------------------
execute TriggerShutNoShutBgp                                                                | PASS |
----------------------------------------------------------------------------------------------------
profile bgp & ospf on All as post trigger snapshot and compare to pre trigger snapshot      | PASS |
----------------------------------------------------------------------------------------------------
Pyats                                                                                       | PASS |
5 critical tests, 5 passed, 0 failed
5 tests total, 5 passed, 0 failed
=====================================================================================================
Output:  /current/dir/output.xml
Log:     /current/dir/log.html
Report:  /current/dir/report.html
```


#### Congratulations! You've Completed PTS with Genie!
