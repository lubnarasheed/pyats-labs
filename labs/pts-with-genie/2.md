## Step 2. Execute Genie PTS


0. Run Docker container and activate your Python virtual environment inside it

```
# Run Docker container
docker run -it -v ~/genie_learning_lab:/genie_tests ciscotestautomation/pyats bash

# Once inside the Docker container, activate Python virtual environment
cd /pyats
source bin/activate

# Download editors
apt-get install vim
```

Please note: This step is optional if you still have the Docker container launched in the `intro-to-genie` lab Step #4.


1. Navigate to the /genie_tests folder and use an editor to view the script `genie_pts.robot`

```
cd /genie_tests
vim genie_pts.robot
```


2. Examine the contents of the script `genie_pts.robot`

```
# Genie Devnet Lab-5: Running Genie PTS
# =====================================


*** Settings ***
Library        ats.robot.pyATSRobot
Library        genie.libs.robot.GenieRobot
Library        unicon.robot.UniconRobot


*** Variables ***
# Defining variables that can be used elsewhere in the test data.
${testbed}     /genie_tests/default_testbed.yaml


*** TestCases ***
# Creating testcases using available Genie, PyATS & Unicon keywords


# Connect to UUT device using CLI
connect to UUT device
    use genie testbed "${testbed}"
    connect to devices "uut"


# Run PTS on all devices and save it to local variable pre_trigger_snapshot
profile bgp & ospf on All as pre trigger snapshot and compare to golden PTS
    Profile the system for "bgp;ospf" on devices "nx-osv-1;csr1000v-1" at "pre_trigger_snapshot"


# Execute some action that could impact device operational state
execute TriggerShutNoShutBgp
    run trigger "TriggerShutNoShutBgp" on device "uut"


# Run PTS on all devices, save it to local variable post_trigger_snapshot and compare it with pre_trigger_snapshot
profile bgp & ospf on All as post trigger snapshot and compare to pre trigger snapshot
    Profile the system for "bgp;ospf" on devices "nx-osv-1;csr1000v-1" at "post_trigger_snapshot"
    Compare profile "post_trigger_snapshot" with "pre_trigger_snapshot" on devices "nx-osv-1;csr1000v-1"

```

In the first test, we are running the PTS on all devices in the topology for features BGP and OSPF. This system profile snapshot is then saved to a local variable "pre_trigger_snapshot".

We then proceed to perform some action on the device that could potentially change the operational state of the topology. In the example above, we execute a trigger to shutdown and then unshut the BGP instance.

Following execution of the trigger, we run PTS on all devices in the topology for features BGP and OSPF again. This system profile snapshot is now saved to a local variable "post_trigger_snapshot". Using Genie keywords, we can then compare the Python datastructures stored within these 2 variables to validate if the device operational states before and after execution of the trigger stayed the same.

For more information, refer to the documentation on [Genie System Profiling with Robot](https://pubhub.devnetcloud.com/media/pyats-packages/docs/genie/robot/index.html#system-profiling)


3. Run this script by executing the following command and examine the output to see testcase results, HTML log report and XML output.

```
(pyenv) hostname$ robot genie_pts.robot
====================================================================================================
Pyats                                                                         
====================================================================================================
connect to UUT device                                                                       | PASS |
----------------------------------------------------------------------------------------------------
profile bgp & ospf on All and save it as golden PTS                                         | PASS |
----------------------------------------------------------------------------------------------------
profile bgp & ospf on All as pre trigger snapshot and compare to golden PTS                 | PASS |
----------------------------------------------------------------------------------------------------
execute TriggerShutNoShutBgp                                                                | PASS |
----------------------------------------------------------------------------------------------------
profile bgp & ospf on All as post trigger snapshot and compare to pre trigger snapshot      | PASS |
----------------------------------------------------------------------------------------------------
Pyats                                                                                       | PASS |
5 critical tests, 5 passed, 0 failed
5 tests total, 5 passed, 0 failed
=====================================================================================================
Output:  /current/dir/output.xml
Log:     /current/dir/log.html
Report:  /current/dir/report.html
```


#### Congratulations! You've Completed PTS with Genie!
