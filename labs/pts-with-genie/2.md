## Step 2. Execute Genie PTS


0. Run Docker container and activate your Python virtual environment inside it

```
# Run Docker container
docker run -it -v ~/genie_learning_lab:/genie_tests ciscotestautomation/pyats:latest-robot /bin/bash

# Once inside the Docker container, activate Python virtual environment
cd /pyats
source bin/activate
```

Please note: This step is optional if you still have the Docker container launched in the `intro-to-genie` lab Step #4.


1. Navigate to the /genie_tests folder and view the script `genie_pts.robot`

```
cd /genie_tests
cat genie_pts.robot
```


2. Examine the contents of the script `genie_pts.robot`

```
# Genie Devnet Lab-5: Running Genie PTS
# =====================================


*** Settings ***
Library        ats.robot.pyATSRobot
Library        genie.libs.robot.GenieRobot
Library        unicon.robot.UniconRobot


*** Variables ***
# Defining variables that can be used elsewhere in the test data.
${testbed}     /genie_tests/default_testbed.yaml


*** TestCases ***
# Creating testcases using available Genie, PyATS & Unicon keywords


# Connect to UUT and helper device using CLI
connect to UUT and helper device
    use genie testbed "${testbed}"
    connect to devices "uut"
    connect to devices "helper"


# Run PTS for features BGP and OSPF on all devices
# Save it to local variable "pre_trigger_snapshot"
#
profile bgp & ospf on all devices as pre-trigger-snapshot
    Profile the system for "bgp;ospf" on devices "uut;helper" as "pre_trigger_snapshot"


# Execute some action that could impact device operational state
execute TriggerShutNoShutBgp
    run trigger "TriggerShutNoShutBgp" on device "uut"


# Run PTS for features BGP and OSPF on all devices
# Save it to local variable "post_trigger_snapshot"
# Compare it with pre_trigger_snapshot
#
profile bgp & ospf on all devices as post-trigger-snapshot and compare to pre-trigger-snapshot
    Profile the system for "bgp;ospf" on devices "uut;helper" as "post_trigger_snapshot"
    Compare profile "post_trigger_snapshot" with "pre_trigger_snapshot" on devices "uut;helper"
```

In the first test, we are running the PTS on all devices in the topology for features BGP and OSPF. This system profile snapshot is then saved to a local variable "pre_trigger_snapshot".

We then proceed to perform some action on the device that could potentially change the operational state of the topology. In the example above, we execute a trigger to shutdown and then unshut the BGP instance.

Following execution of the trigger, we run PTS on all devices in the topology for features BGP and OSPF again. This system profile snapshot is now saved to a local variable "post_trigger_snapshot". Using Genie keywords, we can then compare the Python datastructures stored within these 2 variables to validate if the device operational states before and after execution of the trigger stayed the same.

For more information, refer to the documentation on [Genie System Profiling with Robot](https://pubhub.devnetcloud.com/media/pyats-packages/docs/genie/robot/index.html#system-profiling)


3. Run this script by executing the following command and examine the output to see testcase results, HTML log report and XML output.

```
(pyats) root@cd999d662bb6:/genie_tests# robot genie_pts.robot
==============================================================================
Genie Pts
==============================================================================
connect to UUT and helper device                                      | PASS |
------------------------------------------------------------------------------
profile bgp & ospf on all devices as pre-trigger-snapshot             | PASS |
------------------------------------------------------------------------------
execute TriggerShutNoShutBgp                                          | PASS |
TriggerShutNoShutBgp has passed
------------------------------------------------------------------------------
profile bgp & ospf on all devices as post-trigger-snapshot and com... | FAIL |
Comparison between post_trigger_snapshot and pre_trigger_snapshot is different for feature 'bgp' for device:

'helper'
info:
 instance:
  default:
   vrf:
    default:
     neighbor:
      10.2.2.2:
       address_family:
        ipv4 unicast:
+         total_memory: 1328
-         total_memory: 1608
------------------------------------------------------------------------------
Genie Pts                                                             | FAIL |
4 critical tests, 3 passed, 1 failed
4 tests total, 3 passed, 1 failed
==============================================================================
Output:  /genie_tests/output.xml
Log:     /genie_tests/log.html
Report:  /genie_tests/report.html
```

View the log and report files created above as mentioned in the 'intro-to-genie' lab.

Note:
- This script demonstrates how Genie PTS can be used to report any operational state changes that may have occured as a consequence of actions performed on a device (in this case a `Trigger`). 

- The snapshot before the trigger had a memory_usage value of 1328 under address-family 'ipv4 unicast'. 

- After executing a `Trigger` (or any other potentially destructive action such as Reload, Switchover, etc.), the Genie PTS can be executed once again and operational state saved once again.

- Following this, the 2 snapshots (before and after the trigger) can be compared to check for changes.


#### Congratulations! You've Completed PTS with Genie!
