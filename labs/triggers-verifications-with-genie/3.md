## Step 3. Execute Genie Triggers


0. Run Docker container and activate your Python virtual environment inside it

```
# Run Docker container
docker run -it -v ~/genie_learning_lab:/genie_tests ciscotestautomation/pyats:latest-robot /bin/bash

# Once inside the Docker container, activate Python virtual environment
root@7bfc4e569568:/pyats# cd /pyats
root@7bfc4e569568:/pyats# source bin/activate
```

Please note: This step is optional if you still have the Docker container launched in the `intro-to-genie` lab Step #4.


1. Navigate to the /genie_tests folder and view the script `genie_triggers.robot`

```
(pyats) root@7bfc4e569568:/pyats# cd /genie_tests
(pyats) root@7bfc4e569568:/pyats# cat genie_triggers.robot
```


2. Examine the contents of the script `genie_triggers.robot`

```
# Genie Devnet Lab-3a: Execute Genie Triggers
# ===========================================


*** Settings ***
Library        ats.robot.pyATSRobot
Library        genie.libs.robot.GenieRobot
Library        unicon.robot.UniconRobot


*** Variables ***
# Defining variables that can be used elsewhere in the test data.
${testbed}     /genie_tests/default_testbed.yaml


*** TestCases ***
# Creating testcases using available Genie, PyATS & Unicon keywords


# Connect to UUT device using CLI
connect to UUT device
    use genie testbed "${testbed}"
    connect to devices "uut"


# Genie Trigger: TriggerShutNoShutBgp
#    1. Connects to UUT device, checks if BGP configuration is present and learns BGP configuration
#    2. Configures 'shutdown' under BGP instance to shutdown BGP
#    3. Verifies BGP instance and BGP neighbors are in 'shutdown' state
#    4. Configures 'no shutdown' under BGP instance
#    5. Verifies BGP instance and BGP neighbors are back up
#
execute TriggerShutNoShutBgp
    run trigger "TriggerShutNoShutBgp" on device "uut"


# Genie Trigger: TriggerUnconfigConfigBgp
#    1. Connects to UUT device, checks if BGP configuration is present, learns BGP configuration and creates a checkpoint
#    2. Configures 'no router bgp <instance>' to remove BGP configuration
#    3. Verifies BGP instance and BGP neighbors are removed from configuration
#    4. Restores configuration back to checkpoint stored in Step #1
#    5. Verifies BGP configuration is restored and BGP neighbors are established
#
execute TriggerUnconfigConfigBgp
    run trigger "TriggerUnconfigConfigBgp" on device "uut"

# Genie Trigger: TriggerDisableEnablePim
#    1. Connects to UUT device, checks if PIM configuration is present, learns PIM configuration and creates a checkpoint
#    2. Configures 'no feature pim' to remove PIM configuration
#    3. Verifies PIM configuration is removed
#    4. Restores configuration back to checkpoint stored in Step #1
#    5. Verifies PIM configuration is restored
#
# Note: This trigger is being executed to illustrate what a 'skipped' or 'failed' testcase would look like.
#		The device does not contain any PIM feature configuration and would not satisfy requirements on Step #1
#
execute TriggerDisableEnablePim
	run trigger "TriggerDisableEnablePim" on device "uut"
```

This is a sample script to execute a `Trigger` on a device using Genie.

Note that we don't specify the device type or operating system when executing the Genie `Trigger`. This is because the package `genie.abstract` uses abstraction based on the device operating system to dynamically "lookup" which OS libraries it would need to load for the trigger (ex: Cisco NX or Cisco XE).

For more information on Abstract and Lookup, in Genie refer to the documentation below:
- [Genie Abstract](https://pubhub.devnetcloud.com/media/pyats-packages/docs/abstract/introduction.html)
- [Genie Lookup](https://pubhub.devnetcloud.com/media/pyats-packages/docs/abstract/lookup_class.html)

The following is a list of all the Triggers (for each OS) available within the `genie.libs.sdk` package that can be executed in the manner shown above:
- [Genie Trigger List](https://pubhub.devnetcloud.com/media/pyats-packages/docs/genie/genie_libs/#/triggers)


3. Run this script by executing the following command and examine the output to see testcase results, HTML log report and XML output.

```
(pyats) root@cd999d662bb6:/genie_tests# robot genie_triggers.robot
==============================================================================
Genie Triggers
==============================================================================
connect to UUT device                                                 | PASS |
------------------------------------------------------------------------------
execute TriggerShutNoShutBgp                                          | PASS |
TriggerShutNoShutBgp has passed
------------------------------------------------------------------------------
execute TriggerUnconfigConfigBgp                                      | PASS |
TriggerUnconfigConfigBgp has passed
------------------------------------------------------------------------------
execute TriggerDisableEnablePim                                       | FAIL |
TriggerDisableEnablePim has failed
------------------------------------------------------------------------------
Genie Triggers                                                        | PASS |
4 critical tests, 3 passed, 1 failed
4 tests total, 3 passed, 1 failed
==============================================================================
Output:  /genie_tests/output.xml
Log:     /genie_tests/log.html
Report:  /genie_tests/report.html
```

View the log and report files created above using any browser as mentioned in the 'intro-to-genie' lab.

NOTE:
* For the skipped/failed trigger - check the logs for Step #1 `verify_prerequisite`.
* This section log provides details on how Genie is not able to find any PIM configuration on the device and therefore skips execution of the trigger.


#### Next Step: Genie Verifications
