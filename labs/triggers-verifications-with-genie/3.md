## Step 3. Execute Genie Verifications


0. Run Docker container and activate your Python virtual environment inside it

```
# Run Docker container
docker run -it -v ~/genie_learning_lab:/genie_tests ciscotestautomation/pyats bash

# Once inside the Docker container, activate Python virtual environment
cd /pyats
source bin/activate

# Download editors
apt-get install vim
```

Please note: This step is optional if you still have the Docker container launched in the `intro-to-genie` lab Step #4.


1. Navigate to the /genie_tests folder and use an editor to view the script `genie_verifications.robot`

```
cd /genie_tests
vim genie_verifications.robot
```


2. Examine the contents of the script `genie_verifications.robot`

```
# Genie Devnet Lab-3b: Execute Genie Verifications
# ================================================


*** Settings ***
Library        ats.robot.pyATSRobot
Library        genie.libs.robot.GenieRobot
Library        unicon.robot.UniconRobot


*** Variables ***
# Defining variables that can be used elsewhere in the test data.
${testbed}     /genie_tests/default_testbed.yaml


*** TestCases ***
# Creating testcases using available Genie, PyATS & Unicon keywords


# Connect to UUT device using CLI
connect to UUT device
    use genie testbed "${testbed}"
    connect to devices "uut"


# Run Genie Verification: Verify_BgpAllNexthopDatabase before Trigger
#    1. Connect to the device and execute 'show bgp all nexthop database'
#	 2. Create Python datastructure to save parsed output of show command (pre trigger snapshot)
#
execute Verify_BgpAllNexthopDatabase before trigger
    run verification "Verify_BgpAllNexthopDatabase" on device "uut"


# Execute Genie Trigger: TriggerShutNoShutBgp
#    1. Connects to UUT device and learns BGP configuration
#    2. Configures 'shutdown' under BGP instance to shutdown BGP
#    3. Verifies BGP instance and BGP neighbors are in 'shutdown' state
#    4. Configures 'no shutdown' under BGP instance
#    5. Verifies BGP instance and BGP neighbors are back up
#
execute TriggerShutNoShutBgp
    run trigger "TriggerShutNoShutBgp" on device "uut"


# Run Genie Verification: Verify_BgpAllNexthopDatabase after Trigger
#    1. Connect to the device and execute 'show bgp all nexthop database'
#	 2. Create Python datastructure to save parsed output of show command (post trigger snapshot)
#
execute Verify_BgpAllNexthopDatabase after trigger
    run verification "Verify_BgpAllNexthopDatabase" on device "uut"


# Perform Checks: Verify number of BGP neighbors after trigger is the same as that before the trigger
verify number of bgp neighbors
    verify count "6" "bgp neighbors" on device "uut"
```

This is a sample script to run Genie `Verifications` on a device before and after the execution of a `Trigger`, and then validate the state by comparing values between the Verification snapshots before/after the trigger.

Note that we don't specify the device type or operating system for the Genie trigger we want to execute. This is because the package `genie.abstract` can automatically "learn" what device and OS we are connected to. Based on that knowledge we can "lookup" which libraries we would need to load (ex: Cisco NX or Cisco XE).

For more information on Abstract and Lookup, in Genie refer to the documentation below:
- [Genie Abstract](https://pubhub.devnetcloud.com/media/pyats-packages/docs/abstract/introduction.html)
- [Genie Lookup](https://pubhub.devnetcloud.com/media/pyats-packages/docs/abstract/lookup_class.html)

The following is a list of all the Verifications (for each OS) available within the `genie.libs.sdk` package that can be executed in the manner shown above:
- [Genie Verifications List](https://pubhub.devnetcloud.com/media/pyats-packages/docs/genie/genie_libs/#/verifications)


3. Run this script by executing the following command and examine the output to see testcase results, HTML log report and XML output.

```
(pyats)$ robot genie_verifications.robot
============================================================================
Pyats
============================================================================
connect to UUT device                                               | PASS |
----------------------------------------------------------------------------
execute Verify_BgpAllNexthopDatabase before trigger                 | PASS |
----------------------------------------------------------------------------
execute TriggerShutNoShutBgp                                        | PASS |
----------------------------------------------------------------------------
execute Verify_BgpAllNexthopDatabase after trigger                  | PASS |
----------------------------------------------------------------------------
verify number of bgp neighbors                                      | PASS |
----------------------------------------------------------------------------
verify number of bgp routes                                         | PASS |
----------------------------------------------------------------------------
Pyats                                                               | PASS |
6 critical tests, 6 passed, 0 failed
6 tests total, 6 passed, 0 failed
===========================================================================
Output:  /current/dir/output.xml
Log:     /current/dir/log.html
Report:  /current/dir/report.html
```


#### Congratulations! You've Completed Genie Triggers and Verifications!
