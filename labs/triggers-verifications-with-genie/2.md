## Step 2. Execute Genie Triggers


0. Run Docker container and activate your Python virtual environment inside it

```
# Run Docker container
docker run -it -v /genie_learning_labs:/genie_tests ciscotestautomation/pyats bash

# Once inside the Docker container, activate Python virtual environment
cd /pyats
source env.csh
```

Please note: This step is optional if you still have the Docker container launched in `intro-to-genie` lab [Step4]()


1. Create a new file genie_triggers.robot

```
cd genie_tests
touch genie_triggers.robot
```


2. Edit the file genie_triggers.robot and copy the code below into it

```
# Example
# -------
# Genie Devnet Lab-3a: Execute Genie Triggers

*** Settings ***
Library        pyats.robot.pyATSRobot
Library        genie.libs.robot.GenieRobot
Library        unicon.robot.UniconRobot

*** Variables ***
# Defining variables that can be used elsewhere in the test data.
${testbed}     /genie_tests/default_testbed.yaml

*** Test Cases ***
# Creating test cases from available keywords.

# Connect to UUT device using CLI
connect to UUT device
    use genie testbed "${testbed}"
    connect to devices "uut"

# Genie Trigger: TriggerShutNoShutBgp
#    1. Connects to UUT device and learns BGP configuration
#    2. Configures 'shutdown' under BGP instance to shutdown BGP
#    3. Verifies BGP instance and BGP neighbors are in 'shutdown' state
#    4. Configures 'no shutdown' under BGP instance
#    5. Verifies BGP instance and BGP neighbors are back up
#
execute TriggerShutNoShutBgp
    run trigger "TriggerShutNoShutBgp" on device "uut"

# Genie Trigger: TriggerUnconfigConfigBgp
#    1. Connects to UUT device, learns BGP configuration and creates a checkpoint
#    2. Configures 'no router bgp <instance>' to remove BGP configuration
#    3. Verifies BGP instance and BGP neighbors are removed from configuration
#    4. Restores configuration back to checkpoint stored in Step #1
#    5. Verifies BGP configuration is restored and BGP neighbors are established
#
execute TriggerUnconfigConfigBgp
    run trigger "TriggerUnconfigConfigBgp" on device "uut"
```

Note that we don't need to specify the device type or operating system because the package `genie.abstract` can automatically "learn" what device and OS we are connected to. Based on that knowledge it can "lookup" which libraries it would need to load (ex: Cisco NX or Cisco XE).

For more information on Abstract and Lookup, in Genie refer to the documentation below:
- [Genie Abstract](https://pubhub.devnetcloud.com/media/pyats-packages/docs/abstract/introduction.html)
- [Genie Lookup](https://pubhub.devnetcloud.com/media/pyats-packages/docs/abstract/lookup_class.html)

The following is a list of all the Triggers available within the `genie.libs.sdk` package that can be executed in the manner shown above.
- [Genie Trigger List](https://pubhub.devnetcloud.com/media/pyats-packages/docs/genie/genie_libs/#/triggers)


3. Run this script by executing the following command and examine the output to see testcase results, HTML log report and XML output.

```
(pyats)$ robot genie_triggers.robot
============================================================================
Pyats
============================================================================
connect to UUT device                                               | PASS |
----------------------------------------------------------------------------
execute TriggerShutNoShutBgp                                        | PASS |
----------------------------------------------------------------------------
execute TriggerUnconfigConfigBgp                                    | PASS |
----------------------------------------------------------------------------
Pyats                                                               | PASS |
3 critical tests, 3 passed, 0 failed
3 tests total, 3 passed, 0 failed
===========================================================================
Output:  /current/dir/output.xml
Log:     /current/dir/log.html
Report:  /current/dir/report.html
```


#### Next Step: Execute Genie Verifications
